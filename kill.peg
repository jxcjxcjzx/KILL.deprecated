{
function Token(tag, value=undefined, isKeyword = false, isOperator = false) {
	this.tag = tag;
    this.value = value || [];
    this.isKeyword = function() {return isKeyword;};
    this.isOperator = function() {return isOperator;};
};

var keywords = [];

var escape_mapping = {
    'e': "\e",
    "t": "\t",
    "\\": "\\",
    "n": "\n",
    "r": "\r",
    "b": "\b"
};

function keyword(tag) {
    keywords.push(tag);
    return function(value = undefined) {
        return new Token(tag, value, true, false)
    };
}

function operator(tag) {
    return new Token(tag, undefined, false, true);
}

function string(value) {
	return new Token("string", value);
}

function identifier(value, access = undefined) {
    return new Token("id", {object: value, accessor: access});
}

function number(value) {
    return new Token("number", value);
}

var Kif = keyword("if");
var Kthen = keyword("then");
var Kelse = keyword("else");
var Kend = keyword("end");
var Klet = keyword("let");
var Kin = keyword("in");
var Kbegin = keyword("begin");
var Kset = keyword("set!");
var Klambda = keyword("lambda");

var Ktrue = keyword("true"),
    Kfalse = keyword("false"),
    Kquote = keyword("quote")

var Oarrow = operator("->");
var Oassign = operator(":=");

function KillObject(type, value = undefined, iterable = false) {
    this.type = type;
    this.value = value;
    this.iterable = iterable;
}

var klist = function (value) {
    return new KillObject("list", value, true);
};

var kcomprehension = function(expr, binding) {
    return new KillObject("list_comp", {expr:expr, binding:binding}, true);
};

var kunit = function () {
    return new KillObject("unit", "()", false);
};

}

start = stmts

stmts =
  stmt:stmt stmts:(delim+ stmt:stmt {return stmt})* delim?
  {return [stmt].concat(stmts)}

stmt = blank* stmt:(decl / exp) comment? {return stmt;} / comment

comment = blank* t_comment [^#\n]+ {return [];}

decl = let

exp =
  if / begin / set / squote / infixexp

infixexp =
  l:lexp
    {return l}

lexp =
  lambda /
  fexp

if =
  _if:t_if blank* cond:fexp blank* t_then blank* then:fexp blank* t_else blank* _else:fexp
  {return Kif({test: cond, consequence: then, alternative: _else});}

begin =
  t_begin es:(blank* newline? blank* "(" e:exp ")" {return e})+ blank* delim
  { return Kbegin(es);}

fexp =
  l:aexp blank* rs:(blank* f:aexp {return f;})*
{
   return rs.length
          ? rs.reduce(function(l,r){return [l].concat(r);},l)
          : [l];
}

aexp =
  literal /
  var /
  quote

let = l:t_let blank+ bs:bindings
{
	return Klet(bs);
}

bindings =
  b:binding bs:(sep blank* b:binding {return b;})*
  {return [b].concat(bs);}

binding =
  name:identifier blank* op_assign blank* exp:exp
  {return [name, exp];}

lambda =
  ps:param? blank* (op_arrow blank* newline?) blank* exp:exp sep?
  {return Klambda({params: ps?[ps]:[], body: exp});} /
  p:param blank+ exp:exp sep? {return Klambda({params: [p], body: exp});} /
  p:param blank* l:lambda {return Klambda({params: [p], body: l})}

set = set:t_set blank* id:identifier blank+ exp:exp
{
	return Kset({name: id, value: exp});
}

newline = "\n"

param = "\\" id:identifier {return id;}

t_qt = "'"

t_let = "let"
t_if = "if"
t_then = "then"
t_else = "else"
t_begin = "begin"
t_end = "end"
t_set = "set!"
t_in = "in"
t_comment = ";"

op_assign = ":="

op_arrow = "->"

op_access = "#"

blank = [ \t]

delim = "\n"

identifier =
  id:id accessor:accessor_expr*
{ return identifier(id, accessor); }

id = id:[_a-zA-Z?!+\-*/%\^\$\&]+
  &{
  	 if (keywords.indexOf(id.join("")) > -1) {
        //error("keyword "+id.join("") + " cannot be an identifier");
     	return false;
     }
     return true;
   }
{return id.join("")}

accessor_expr = access:op_access method:accessor?
{
    if (method == null) {
       error("must specify accessor after #")
    }
    return method;
}

accessor = access:(id / [0-9]+ )
{
    return typeof(access) == "string"
           ? access
           : access.join("");
}

var = identifier

digits = ds:[0-9]+
{
    return number(parseInt(ds.join(""),10));
}

quote = "(" e:exp es:exp* ")"
  {return es.length? [e].concat(es): [e];}

literal = digits / bools / unit / list / string

list = "[" l:list_item "]" {return klist(l);} / comprehension

comprehension = "[" blank* exp:exp blank* newline? blank*"|" blank* bind:in_bindings blank* "]"
{
    return kcomprehension(
    		bind
                .slice(0)
                .reverse()
                .reduce((a,b) => Klambda({params: [b[0]], body: a}),
                        exp),
    		bind)
}

in_binding =  id:identifier blank* "in" blank* exp:exp cond:(blank* t_if blank* exp:exp blank* {return exp;})?
{
    return {name: id, value: exp, condition: cond};
}

in_bindings = i:in_binding ins:(sep i:in_binding {return i})*
{
    return [i].concat(ins);
}

list_item = exp:exp blank* l:(sep l:list_item {return l;})?
{
    return l
            ?[exp].concat(l)
            :[exp];
}

unit = "(" blank* ")" {return undefined;}

bools = bool:("true" / "false")
{
    return bool == "true" ? Ktrue(true) : Kfalse(false);
}

squote = t_qt exp:exp {return ["quote"].concat(exp);}

string = "\"" str:string_literal "\"" {return string(str);}

string_literal = nd:(non_dquote / escape)* {return nd.join("")}

dquote = "\""

non_dquote= x:[^"\\] {return x;}

escape= "\\" x:escaped {return escape_mapping[x];} /
    "\\u" uni:unicode_seq {return String.fromCodePoint(parseInt(uni, 16));}

escaped = "\\" / "\"" / "b" / "e" / "n" / "r" / "t"

unicode_seq = seq:[a-eA-E-0-9]+ {return seq.join("")}

sep = blank* "," newline? blank*
